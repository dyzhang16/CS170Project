title: ArcadeStart
tags:
---
// WARNING: This will break if DialogueRunner's StartAutomatically field is set
<<if visited("ArcadeStart") is false>>
<<ChangeSpeaker Player>>
    So this is the arcade. All the machines here seem to look the same with the same placeholder art...

    There were three games I wanted to take a look at while I'm here.
<<ChangeVariable GameManager arcadeFirstVisit 1>>
<<endif>>
===

title: PromptCrane
tags:
---
<<ChangeSpeaker Player>>
Do you want to play the crane game?
-> Yes
    <<Show CranePuzzlePanel>>
    <<set $playedCrane to true>>
    [[CraneDirections]]
-> No
    <<stop>>
===

title: CraneDirections
tags:
---
<<ChangeSpeaker Player>>
<<BlockMachine CranePuzzlePanel true>>
<<if $visitedCraneDirections is null>>
    Here are the directions...

    "Insert a coin."

    "Use the joystick to your left to move the claw left or right."

    "Press the Drop button to start lowering the claw."

    "If the claw grabs an item, mash the Willpower button for the claw to keep grabbing on!"

    Hmm, the first few instructions made sense, but what's this about a "Willpower" button? I guess I'll find out.
    <<set $visitedCraneDirections to true>>
<<else>>
    <<if $doNotShowCraneDirs is null>>
        Do you want to read the directions again?
        -> Yes.
            <<set $visitedCraneDirections to null>>
            [[CraneDirections]]
        -> No.
            <<set $doNotShowCraneDirs to true>>
    <<endif>>
<<endif>>
<<BlockMachine CranePuzzlePanel false>>
===

title: PromptBasketball
tags:
---
<<ChangeSpeaker Player>>
Do you want to play basketball?
-> Yes
    <<Show BasketballPanel>>
    <<set $playedBasketball to true>>
-> No
    <<stop>>
===

title: PromptDance
tags:
---
<<ChangeSpeaker Player>>
Do you want to play the dancing game?
-> Yes
    <<Show DanceGamePuzzlePanel>>
    <<set $playedDance to true>>
    [[DanceDirections]]
-> No
    <<stop>>
===

title: DanceDirections
tags:
---
<<ChangeSpeaker Player>>
<<BlockMachine DanceGamePuzzlePanel true>>
<<if $visitedDanceDirections is null>>
    Here are the directions...

    "Each round, you will see arrows pop up on the left."

    "Press the directional buttons on the right matching what you saw."

    "The game gets progressively more difficult the better you do. Good luck!"
    <<set $visitedDanceDirections to true>>
<<else>>
    <<if $doNotShowDanceDirs is null>>
        Do you want to read the directions again?
        -> Yes.
            <<set $visitedDanceDirections to null>>
            [[DanceDirections]]
        -> No.
            <<set $doNotShowDanceDirs to true>>
    <<endif>>
<<endif>>
<<BlockMachine DanceGamePuzzlePanel false>>
===

title: ArcadeLeave
tags:
---
<<if ($playedCrane is true and $playedBasketball is true and $playedDance is true)>>
    <<ChangeSpeaker Player>>
    Do you want to go exit?
    -> Yes.
        // <<ExitGame>>
        <<ChangeScene BlackScreen CityArcade>>
    -> No.
        <<exit>>
<<else>>
    <<ChangeSpeaker Player>>
    There are still some games I haven't tried yet.
<<endif>>
===

title: ArcadeUnusable
tags:
---
<<ChangeSpeaker Player>>
    Looks like I can't use this machine.
<<exit>>
===

title: PrizeBooth
tags:
---
<<ChangeSpeaker Player>>
    This is the prize booth. Too bad I don't have any tickets.
<<exit>>
===